# Maintainer: amadejk <amadejkastelic7@gmail.com>
pkgname=hyprlux
pkgver=__VERSION__
pkgrel=__PKGREL__
pkgdesc="Hyprland utility that automates vibrance and night light control"
arch=('x86_64')
url="https://github.com/amadejkastelic/Hyprlux"
license=('MIT')
depends=('hyprland')
makedepends=('cargo')
install="install.sh"
options=('!debug')
source=(
    "Hyprlux-${pkgver}.tar.gz::https://github.com/amadejkastelic/Hyprlux/archive/refs/tags/${pkgver}.tar.gz"
    "hyprlux.service"
    "config.toml"
)
sha256sums=(
    '__TARBALL_SHA__'
    '__SERVICE_SHA__'
    '__CONFIG_SHA__'
)

prepare() {
    cd "$srcdir/Hyprlux-${pkgver}"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's|host: ||p')"
}

build() {
    cd "$srcdir/Hyprlux-${pkgver}"
    cargo build --frozen --release --target-dir target
}

check() {
    cd "$srcdir/Hyprlux-${pkgver}"
    cargo test --frozen --release
}

package() {
    cd "$srcdir/Hyprlux-${pkgver}"
    install -Dm755 "target/release/hyprlux" "$pkgdir/usr/bin/hyprlux"
    install -Dm644 "$srcdir/hyprlux.service" "$pkgdir/usr/lib/systemd/system/hyprlux.service"
    install -Dm644 "$srcdir/config.toml" "$pkgdir/usr/share/hyprlux/config.toml"
}